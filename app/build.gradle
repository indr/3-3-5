apply plugin: 'com.android.application'

repositories {
}

android {
    // Picking your compileSdkVersion, minSdkVersion, targetSdkVersion
    // https://medium.com/google-developers/picking-your-compilesdkversion-minsdkversion-targetsdkversion-a098a0341ebd
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "ch.indr.threethreefive"
        minSdkVersion 16
        //noinspection OldTargetApi
        targetSdkVersion 22
        versionCode Integer.parseInt(System.getenv("BITRISE_BUILD_NUMBER") ?: "1")
        versionName "0.5.0." + (System.getenv("BITRISE_BUILD_NUMBER") ?: "0")
        setProperty("archivesBaseName", "threethreefive-$versionName")

        // Enable Multidex to support over 65K methods
        multiDexEnabled true

        buildConfigField "boolean", "ANSWERS", "true"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "FEATURE_DONATE_TO", "false"
        }
        debug {
            applicationIdSuffix '.debug'
            buildConfigField "boolean", "ANSWERS", "false"
            buildConfigField "boolean", "FEATURE_DONATE_TO", "true"
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    lintOptions {
        abortOnError false
        lintConfig rootProject.file('gradle/lint.xml')
    }
}

configurations {
    // WARNING: Dependency org.apache.httpcomponents:httpclient:4.0.1 is ignored for release as it
    // may be conflicting with the internal version provided by Android.
    // In case of problem, please repackage it with jarjar to change the class packages
    // http://stackoverflow.com/a/41379193
    compile.exclude group: "org.apache.httpcomponents", module: "httpclient"

    // Execution failed for task ':app:transformClassesWithJarMergingForDebug'.
    // > com.android.build.api.transform.TransformException: java.util.zip.ZipException: duplicate entry: org/apache/commons/io/CopyUtils.class
    // http://stackoverflow.com/a/42982662
    compile.exclude group: "org.apache.commons"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:gridlayout-v7:25.3.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:support-annotations:25.3.1'
    compile 'com.github.frankiesardo:auto-parcel:0.3.1'
    annotationProcessor 'com.github.frankiesardo:auto-parcel-processor:0.3.1'
    compile 'com.google.dagger:dagger:2.4'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.4'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.jakewharton.timber:timber:3.0.1'
    compile 'com.amitshekhar.android:android-networking:1.0.0'
    compile 'com.amitshekhar.android:rx-android-networking:1.0.0'
    compile 'com.octo.android.robospice:robospice:1.4.14'
    compile 'com.octo.android.robospice:robospice-google-http-client:1.4.14'
    compile('com.google.http-client:google-http-client-jackson:1.22.0') {
        exclude module: 'xpp3'
        exclude group: 'stax'
    }
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.trello:rxlifecycle-components:0.3.0'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.3.0'
    // Testing
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.7.17"
    testCompile 'org.robolectric:robolectric:3.1.4'
    testCompile 'org.robolectric:shadows-multidex:3.0'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.7.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
